buildscript {
    repositories {
        mavenCentral() // or jcenter()
    }

    dependencies {
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.6'
    }
}

apply plugin: 'project-report'
apply plugin: 'com.android.library'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion android_sdk
    buildToolsVersion buildTools_version

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion android_sdk

        // Enabling multidex support.
        //        multiDexEnabled true
        // Fetch the version according to git latest tag and "how far are we from last tag"
        (versionCode, versionName) = rootProject.getVersionCodeAndName("default")
        printf("\n-------- VERSION DATA for '$project.name' --------" +
                "\n" +
                "- CODE: " +
                versionCode +
                "\n" +
                "- NAME: " +
                versionName +
                "\n----------------------------\n")
    }

    //Those files, from Apache Commons lib cause build errors
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
        disable 'TypographyEllipsis'
    }


    buildTypes {
        release {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            zipAlignEnabled true
        }

        debug {
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
}

//Automate the way version name is built for each flavor
android.productFlavors.each {
    flavor ->
        def (versionCode, versionName) = rootProject.getVersionCodeAndName(flavor.name)
        flavor.versionCode = versionCode
        flavor.versionName = versionName
}

dependencies {
    implementation project(":libraries:parcelable")
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:1.0.0"
}
